buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
    }
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'com.github.johnrengelman.shadow'

ext.vertxVersion = '3.1.0'
version = '0.0.1'

if (!JavaVersion.current().java8Compatible) {
    throw new IllegalStateException('''A Haiku:
                                      |  This needs Java 8,
                                      |  You are using something else,
                                      |  Refresh. Try again.'''.stripMargin())
}

repositories {
    mavenCentral()
    maven {
        url = 'http://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    compile "io.vertx:vertx-core:$vertxVersion"
    compile "io.vertx:vertx-lang-groovy:$vertxVersion", {
        exclude module: 'groovy-all'
    }
    compile 'org.codehaus.groovy:groovy-all:2.4.5'
    compile "io.vertx:vertx-web:$vertxVersion"
    compile "io.vertx:vertx-jdbc-client:$vertxVersion"
    compile "io.vertx:vertx-rx-groovy:$vertxVersion"
    compile "io.vertx:vertx-auth-jwt:$vertxVersion"

    compile 'com.h2database:h2:1.4.190'

    testCompile "junit:junit:4.12"
    testCompile "io.vertx:vertx-unit:$vertxVersion"

    runtime 'ch.qos.logback:logback-classic:1.1.3'
}

processResources {
    from 'src/main/groovy'
}

shadowJar {
    classifier = 'fat'
    manifest {
        attributes 'Main-Class': 'io.vertx.core.Launcher'
        attributes 'Main-Verticle': 'groovy:com.alvarosanchez.teams.vertx.TeamsVerticle'
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

test {
    testLogging {
        exceptionFormat = 'full'
        events "failed", "standardOut", "standardError"
        outputs.upToDateWhen { false }
    }
}
